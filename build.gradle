plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.springdoc.openapi-gradle-plugin" version "1.9.0"
    id 'com.github.johnrengelman.shadow' version '8.1.1' // üëà Plugin para empaquetar Lambda correctamente
}

group = 'co.edu.udea.nexum'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    set('springCloudVersion', "2024.0.1")
}

dependencies {
    // AWS Lambda + Spring Boot integration
    implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot3:2.0.2'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'

    // MapStruct y Lombok
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    implementation 'org.mapstruct:mapstruct:1.6.3'
    compileOnly 'org.projectlombok:lombok'

    // Feign
    implementation 'io.github.openfeign:feign-hc5:13.5'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // Base de datos
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.h2database:h2'

    // Jasper Reports
    implementation 'net.sf.jasperreports:jasperreports:7.0.3'
    implementation 'net.sf.jasperreports:jasperreports-pdf:7.0.3'
    implementation 'net.sf.jasperreports:jasperreports-fonts:7.0.3'
    implementation 'org.jfree:jfreechart:1.5.6'
    implementation 'commons-logging:commons-logging:1.2'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('bootRun') {
    doFirst {
        file('.env').readLines().each() {
            def (key, value) = it.tokenize('=')
            environment key, value
        }
    }
}

import java.nio.file.Files

// Task para preparar el application.yml con variables del .env
task prepareLambdaConfig {
    doLast {
        def envFile = file(".env")
        def sourceFile = file("src/main/resources/application.yml")
        def targetDir = file("$buildDir/generated-resources")
        def targetFile = new File(targetDir, "application.yml")

        targetDir.mkdirs()
        def content = sourceFile.text

        if (envFile.exists()) {
            println "üîç Leyendo variables del .env"
            envFile.readLines().each { line ->
                if (line && line.contains('=')) {
                    def (key, value) = line.tokenize('=')
                    value = value.trim()
                    content = content.replace("\${${key}}", value)
                }
            }
        }

        targetFile.text = content
        println "‚úÖ application.yml preparado con valores del .env para Lambda"
    }
}

// ‚öôÔ∏è Shadow JAR: empaqueta TODAS las dependencias y deja las clases en la ra√≠z
tasks.named('shadowJar') {
    dependsOn prepareLambdaConfig
    archiveBaseName.set('profile-lambda')
    archiveClassifier.set('')
    archiveVersion.set('')
    from("$buildDir/generated-resources") {
        into ''
    }
    mergeServiceFiles()
}

tasks.named('build') {
    dependsOn tasks.named('shadowJar')
}
