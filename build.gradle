plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.springdoc.openapi-gradle-plugin" version "1.9.0"
}

group = 'co.edu.udea.nexum'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
	set('springCloudVersion', "2024.0.1")
}

dependencies {
	// Annotations
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: '1.6.3'

	implementation group: 'org.mapstruct', name: 'mapstruct', version: '1.6.3'
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.5'

	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: '3.4.3'
	implementation 'com.h2database:h2'

	// Feign
	implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.2.0'
	implementation group: 'io.github.openfeign', name: 'feign-hc5', version: '13.5'

	// Developer
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'


	// JsonWebToken
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.6'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.12.6'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.12.6'

	// Database
	runtimeOnly 'org.postgresql:postgresql'

	// Reports
	implementation group: 'net.sf.jasperreports', name: 'jasperreports', version: '7.0.3'
	implementation group: 'net.sf.jasperreports', name: 'jasperreports-pdf', version: '7.0.3'
	implementation 'net.sf.jasperreports:jasperreports-fonts:7.0.3'
	implementation group: 'org.jfree', name: 'jfreechart', version: '1.5.6'
	implementation 'commons-logging:commons-logging:1.2'

	// Email
	implementation 'com.brevo:brevo:1.1.0'

	// Fuzzy Search
	implementation 'org.apache.commons:commons-text:1.12.0'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

def jacocoExclude = [
		'**/dto/.*',
		'**/spi/.*',
		'**/api/.*',
		'**/mappers/.*',
		'**/repository/.*',
		'**/*.test',
		'**/generated/.*',
		"**/model/**/.*",
		"**/application/**/.*",
		"**/infrastructure/**/.*",
]

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.12"
}


jacocoTestReport {
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: jacocoExclude)
		}))
	}
}

tasks.named('bootRun') {
    doFirst {
        file('.env').readLines().each() {
            def (key, value) = it.tokenize('=')
            environment key, value
        }
    }
    jvmArgs([
      '-Xmx512m'
    ])
}
